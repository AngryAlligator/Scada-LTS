/*
 *
 * BETA FEATURE (not-stable, still under development)
 * We advise to use ANT build instead of gradle.
 *
 * Scada-LTS gradle build configuration
 * Based on the ANT "build.xml" file. This file provide the information
 * where the source set is located, where are located the java libraries
 * and use subproject tasks to include an NewUI contents inside Scada-LTS app.
 */

plugins {
    id 'war'
    id 'java'
}


sourceCompatibility = 1.11
targetCompatibility = 1.11

def tomcatVersion = '9.0.41'
def tomcatDirectory = '/opt/tomcat/9.0.41'

// Environment variables to change
subprojects {
    ext {
        buildUi = true
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    providedCompile fileTree(include: ['*.jar'], dir:'WebContent/WEB-INF/lib')
    providedCompile fileTree(include: ['*.jar'], dir: 'lib')
    providedCompile 'org.apache.tomcat:tomcat-catalina:' + tomcatVersion
}
sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
    }
    test {
        java {
            srcDirs = ['test']
        }
    }
}

war {
    dependsOn(':scadalts-ui:copyNewUiImages')
    from 'WebContent'
}

task clearDockerDir(type: Delete) {
    delete files("docker/volumes/webapps/ScadaBR.war")
}
task copyWarToDocker(type: Copy, dependsOn: clearDockerDir) {
    from "build/libs/"
    into "docker/volumes/webapps/"
    include('Scada-LTS.war')
    rename('Scada-LTS.war', "ScadaBR.war")
}

task installWebDependency(type: Exec) {
    workingDir "./WebContent/resources"
    commandLine "npm", "install"
}

task run(type: Exec) {
    workingDir tomcatDirectory + '/bin'
    commandLine 'sh','-c', './catalina.sh', 'run'
}